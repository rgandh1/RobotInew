*** Settings ***
Documentation     This is an end-to-end testing implementation of the IN_BST_005 - MOC to Forbidden Number TC
...               ATTENTION: AndroidRemoting is not able to dedect an IVR announcement played as ringbactone (no ISUP ANswerMessage)
...                          Thus this test suite can NOT distinguish wether or not the expected IVR announcement is played
...                          between DIALING and the expacted CONNECTION timeout. 
...                          We assume that this happended but manual testing is necessary in case
...                          - no connection dedection is not enough to indicate correct forbidden number handling
...                          - it is necessary to even verify that not only ANY but a certain announcement was played 
...               ---------------------------------------------------------------
...               Run MOC Forbidden Number Format1
...               Run MOC Forbidden Number Format2
...               Run MOC Forbidden Number Format3
...
...               ===============================================================================
...               Run with arguments:
...               --variable ROBOT_WORKSPACE_RESOURCE:/opt/robot/robotworkspace/robotworkspace-resource.txt 
...               --variable SUT_RESOURCE:/opt/robot/robotworkspace/suts/MEX-resource.txt
...               ============================================================
Force Tags        FALABELLA_CHILE_supported  walter.heincz  End2EndTest
Resource          ${ROBOT_WORKSPACE_RESOURCE}  #Be aware that variables imported with a resource file are NOT visible in the local Variables table (but in all other local tables)
Resource          ${SUT_RESOURCE}
Resource          ${ROBOT WORKSPACE PATH}/libs/global-keyword-resources/android-remoting/ars_support-resource.txt
Resource          ${ROBOT WORKSPACE PATH}/libs/global-keyword-resources/crce-subscriberadmin/subscriber-admin-support-resource.txt
Resource          ../../configuration-resource.txt
Resource          ../../suite-global-keyword-resource.txt
Resource          ${ROBOT WORKSPACE PATH}/libs/global-keyword-resources/crm-support/crm_support-resource.txt
Resource          ${ROBOT WORKSPACE PATH}/libs/global-keyword-resources/cg-support/cg_support-resource.txt
Library           OperatingSystem
#Library          #Selenium2Library
Library           ${ROBOT WORKSPACE PATH}/libs/py/InewSelenium2Library.py

Suite Setup       Setup Suite        
Suite Teardown    Teardown Suite



*** Variables ***

${CALL_DURATION_MILLIS}=              1000  #1 second

${NEWLINE}=  \n

#${CRM_PORTAL_LOGIN_URL}=             #ia initialized by SUITE SETUP
#${CRM_PORTAL_SUBSCRIBER_INPUT_URL}=  #ia initialized by SUITE SETUP



*** Keywords ***


Run Voice Call Phone 1 To Forbidden Number 
    [Documentation]  executes a Phone 1 call to a passed forbidden msisdn 
    ...
    ...              Arguments:
    ...                dial_msisdn            Forbidden MSISDN that shall be dialed on the calling phone 
    ...
    ...              Returns:                 Nothing - the function fails if the call DID succeed - i.e. a connection was established instead of rerouting to (no ISUP ANswerMessage sending) IVR announcement
    ...              Author: walter.heincz 
    ...              =========================================================
    [Arguments]    ${dial_msisdn}  
    ${resultCode}=  ARS Voice Call To Unattached Phone            ${ANDROID REMOTING HOST}    ${ANDROID REMOTING PORT}    ${ATTACHED_ONNET_PHONE_1_IMSI}    ${dial_msisdn}    ${CALL_DURATION_MILLIS}   
                     Should Be Equal As Strings            ${resultCode}              CONNECT_FAILED       msg=Call to forbidden number was expected to stay in DIALING mode (non-ISUPAnswer sending IVR announcement) and thus fail due to CONNECT_FAILED
    Run Keyword If   '${FAKE_CALLS}'=='true'               Fake Forbidden Number Call Impact On Testbed



Run Forbidden Number Call And Check Phone 1 Ticket
    [Documentation]  Runs a Phone 1 call to the passed forbidden number and verifies that a CRM "MOC forbidden" ticket was written
    ...
    ...              Arguments:               
    ...                dial_msisdn       
    ...
    ...              Returns:                 Nothing - the function fails if the call or CRM query doesn't succeed
    ...              Author: walter.heincz 
    ...              =========================================================
    [Arguments]  ${dial_msisdn}
    ${phone1_imsi}=  Run Keyword If  '${FAKE_CALLS}'=='true'      Set Variable             ${FAKE_ATTACHED_ONNET_PHONE_1_IMSI}      #On faked calls replace the ${ATTACHED_ONNET_PHONE_1_IMSI} with the faked call subscriber imsi
                     ...             ELSE                         Set Variable             ${ATTACHED_ONNET_PHONE_1_IMSI}
    ${marker_ticket_timestamp}       ${type_dummy}=               CRM Get Latest Ticket    ${phone1_imsi}
                     Run Voice Call Phone 1 To Forbidden Number   ${dial_msisdn}
                     CRM New Ticket Should Exist                  ${phone1_imsi}           ${marker_ticket_timestamp}    ticket_type=Voice     ticket_channel=MOC forbidden



Fake Forbidden Number Call Impact On Testbed
    [Documentation]  Runs a CG MOC FinalRerouting session for the faked phone 1 to the faked forbidden number
    ...              This FAKING strategy is neccessary during suite development time 
    ...              when there is no connection between the Android Phones's mobile network
    ...              and the testbed.
    ...              By faking the Android call's impact on the testbed with a CG session 
    ...              we can verify all other aspects of the test suite apart from 
    ...              the Android Remoting functionality.
    ...              Arguments:               None
    ...              Returns:                 Nothing - the function fails if the CG call doesn't succeed
    ...              Author: walter.heincz 
    ...              =========================================================
    ${rc}=           CG Run MOC Final Rerouting    ${FAKE_ATTACHED_ONNET_PHONE_1_IMSI}    ${FAKE_ATTACHED_ONNET_PHONE_1_MSISDN}    ${FAKE_MOC_FORBIDDEN_NUMBER}    ${MSC ADDRESS}    ${CALLSGEN HOME}    ${CALLSGEN JAR NAME}    ${CG UNIFIED WS PATH}    ${AMQ-SIG HOST}   ${AMQ-SIG PORT}    ${AMQ-SIG MOC REQ QUEUE}    ${AMQ-SIG MOC RES QUEUE}                      
                     Should be equal as Integers  ${rc}  0   msg=Faking CG session failed





*** Test Cases ***



Run MOC Forbidden Number Format1
    [Documentation]  Runs a call on phone 1 to the forbidden number format 1
    ...              Plus verification that "MOC forbidden" ticket was written for the Phone 1 subscriber
    ...              =========================================================
    [Setup]          Open Browser    ${CRM_PORTAL_LOGIN_URL}    firefox
                     Run Forbidden Number Call And Check Phone 1 Ticket    ${MOC_FORBIDDEN_NUMBER_FORMAT1}
    [Teardown]       Close Browser    

Run MOC Forbidden Number Format2
    [Documentation]  Runs a call on phone 1 to the forbidden  numberr format 2
    ...              =========================================================
    [Setup]          Open Browser    ${CRM_PORTAL_LOGIN_URL}    firefox
                     Run Forbidden Number Call And Check Phone 1 Ticket    ${MOC_FORBIDDEN_NUMBER_FORMAT2}
    [Teardown]       Close Browser    

Run MOC Forbidden Number Format3
    [Documentation]  Runs a call on phone 1 to the forbidden  number format 3
    ...              =========================================================
    [Setup]          Open Browser    ${CRM_PORTAL_LOGIN_URL}    firefox
                     Run Forbidden Number Call And Check Phone 1 Ticket    ${MOC_FORBIDDEN_NUMBER_FORMAT3}
    [Teardown]       Close Browser    


