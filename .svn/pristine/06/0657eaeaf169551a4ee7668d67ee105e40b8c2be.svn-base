*** Settings ***
Documentation     This is an end-to-end testing implementation of the IN_PP_MOC_002 - Prepaid MOC - No Credit TC
...               ---------------------------------------------------------------
...               Run Prepaid MOC No Credit
...
...               ===============================================================================
...               Run with arguments:
...               --variable ROBOT_WORKSPACE_RESOURCE:/opt/robot/robotworkspace/robotworkspace-resource.txt 
...               --variable SUT_RESOURCE:/opt/robot/robotworkspace/suts/MEX-resource.txt
...               ============================================================
Force Tags        TNZ_supported  walter.heincz  End2EndTest
Resource          ${ROBOT_WORKSPACE_RESOURCE}  #Be aware that variables imported with a resource file are NOT visible in the local Variables table (but in all other local tables)
Resource          ${SUT_RESOURCE}
Resource          ${ROBOT WORKSPACE PATH}/libs/global-keyword-resources/android-remoting/ars_support-resource.txt
Resource          ${ROBOT WORKSPACE PATH}/libs/global-keyword-resources/crce-subscriberadmin/subscriber-admin-support-resource.txt
Resource          ../../configuration-resource.txt
Resource          ../../suite-global-keyword-resource.txt
Resource          ${ROBOT WORKSPACE PATH}/libs/global-keyword-resources/crm-support/crm_support-resource.txt
Resource          ${ROBOT WORKSPACE PATH}/libs/global-keyword-resources/cg-support/cg_support-resource.txt
Library           OperatingSystem
#Library          #Selenium2Library
Library           ${ROBOT WORKSPACE PATH}/libs/py/InewSelenium2Library.py

Suite Setup       Setup Suite        
Suite Teardown    Teardown Suite



*** Variables ***

${CALL_DURATION_MILLIS}=              1000  #1 second

${NEWLINE}=  \n

#${CRM_PORTAL_LOGIN_URL}=             #ia initialized by SUITE SETUP
#${CRM_PORTAL_SUBSCRIBER_INPUT_URL}=  #ia initialized by SUITE SETUP



*** Keywords ***

Setup Testcase
    [Documentation]  Extracts the TC's complex setup into a separate keyword due to the limitations of the [Setup] tag
    ...
    ...              Arguments:  None
    ...              Returns:    Nothing 
    ...              Author: walter.heincz 
    ...              =========================================================
                     Run Keyword If  '${FAKE_CALLS}'=='true'      CRCE Set Main Balance By Imsi      ${CRCE01 HOST}    ${CRCE01 WEBSERVICES PORT}    ${CRCE01 WEBSERVICES BASE PATH}    ${FAKE_ATTACHED_ONNET_PHONE_1_IMSI}  0
                     ...       ELSE                               CRCE Set Main Balance By Imsi      ${CRCE01 HOST}    ${CRCE01 WEBSERVICES PORT}    ${CRCE01 WEBSERVICES BASE PATH}    ${ATTACHED_ONNET_PHONE_1_IMSI}       0     
                     Open Browser    ${CRM_PORTAL_LOGIN_URL}    firefox                

Teardown Testcase
    [Documentation]  Extzracts the TC's complex terdow into a separate keyword due to the limitations of the [Teardown] tag
    ...
    ...              Arguments:  None
    ...              Returns:    Nothing 
    ...              Author: walter.heincz 
    ...              =========================================================
                     Run Keyword If  '${FAKE_CALLS}'=='true'      CRCE Set Main Balance By Imsi      ${CRCE01 HOST}    ${CRCE01 WEBSERVICES PORT}    ${CRCE01 WEBSERVICES BASE PATH}    ${FAKE_ATTACHED_ONNET_PHONE_1_IMSI}  10000000     
                     ...       ELSE                               CRCE Set Main Balance By Imsi      ${CRCE01 HOST}    ${CRCE01 WEBSERVICES PORT}    ${CRCE01 WEBSERVICES BASE PATH}    ${ATTACHED_ONNET_PHONE_1_IMSI}       10000000        
                     Close Browser                



Run IVR Rerouting Voice Call   
    [Documentation]  executes a voice call between 2 Phones expecting to be redirected to IVR due to insufficient funds
    ...
    ...              Arguments:
    ...                cg_device_imsi         IMSI of the phone that shall act as the calling phone
    ...                cd_device_imsi         IMSI of the phone that should be called when dialMsisdn (see below) is dialed on the calling phone and the calling subcriber has sufficient funds
    ...                dial_msisdn            MSISDN that shall be dialed on the calling phone (specified via cgDeviceImsi).
    ...
    ...              Returns:                 Nothing - the function fails if the call SUCCEEDS
    ...              Author: walter.heincz 
    ...              =========================================================
    [Arguments]  ${cg_device_imsi}  ${cd_device_imsi}  ${dial_msisdn}  
    ${dial_msisdn}=  Run Keyword If  '${FAKE_CALLS}'=='true'      Set Variable             ${FAKE_IVR_REDIRECTION_NUMBER}      #On faked calls replace the passed dial_msisdn with any announcement 
                     ...       ELSE                               Set Variable             ${dial_msisdn}                      # auto-assignement necessary - otherwise None will be assigned
    ${resultCode}=  ARS Voice Call                        ${ANDROID REMOTING HOST}    ${ANDROID REMOTING PORT}    ${cg_device_imsi}    ${cd_device_imsi}    ${dial_msisdn}    ${CALL_DURATION_MILLIS}    released_by_cg=true
                    Should Be Equal As Strings            ${resultCode}              CALLED_DEVICE_NOT_RINGING    msg=Expectation that when dialing on calling device the called device will NOT ring is not confirmed  
    Run Keyword If   '${FAKE_CALLS}'=='true'              Fake Insufficient Funds Call Impact On Testbed


Run IVR Rerouting Call And Check Nonexistent Ticket
    [Documentation]  Runs IVR Rerouting Call Phone 1 to Phone 2 
    ...              and verifies that NO ticket was written
    ...
    ...              Arguments:  None      
    ...              Returns:    Nothing - the function fails if the call or CRM query doesn't succeed
    ...              Author: walter.heincz 
    ...              =========================================================
    ${phone1_imsi}=  Run Keyword If  '${FAKE_CALLS}'=='true'      Set Variable             ${FAKE_ATTACHED_ONNET_PHONE_1_IMSI}      #On faked calls replace the ${ATTACHED_ONNET_PHONE_1_IMSI} with the faked call subscriber imsi
                     ...             ELSE                         Set Variable             ${ATTACHED_ONNET_PHONE_1_IMSI}
    ${marker_ticket_timestamp}       ${type_dummy}=               CRM Get Latest Ticket    ${phone1_imsi}
                     Run IVR Rerouting Voice Call   ${ATTACHED_ONNET_PHONE_1_IMSI}    ${ATTACHED_ONNET_PHONE_2_IMSI}   ${ATTACHED_PHONE_2_MSISDN_FORMAT1}
                     CRM New Ticket Should Not Exist              ${phone1_imsi}           ${marker_ticket_timestamp}    ticket_type=Voice     



Fake Insufficient Funds Call Impact On Testbed
    [Documentation]  Runs a CG MOC Final Redrouting session for the faked phone 1 to the faked phone 2
    ...              This FAKING strategy is neccessary during suite development time 
    ...              when there is no connection between the Android Phones's mobile network
    ...              and the testbed.
    ...              By faking the Android call's impact on the testbed with a CG session 
    ...              we can verify all other aspects of the test suite apart from 
    ...              the Android Remoting functionality.
    ...              Arguments:               None
    ...              Returns:                 Nothing - the function fails if the CG call doesn't succeed
    ...              Author: walter.heincz 
    ...              =========================================================
    ${rc}=           CG Run MOC Final Rerouting    ${FAKE_ATTACHED_ONNET_PHONE_1_IMSI}    ${FAKE_ATTACHED_ONNET_PHONE_1_MSISDN}    ${FAKE_DESTINATION_MSISDN}    ${MSC ADDRESS}    ${CALLSGEN HOME}    ${CALLSGEN JAR NAME}    ${CG UNIFIED WS PATH}    ${AMQ-SIG HOST}   ${AMQ-SIG PORT}    ${AMQ-SIG MOC REQ QUEUE}    ${AMQ-SIG MOC RES QUEUE}                      
                     Should be equal as Integers  ${rc}  0   msg=Faking CG session failed





*** Test Cases ***



Run Prepaid MOC No Credit
    [Documentation]  Sets the phone 1 subcriber to a low balance and then tries a call to phone 2
    ...              Plus verification that no ticket was written for the Phone 1 subscriber
    ...              =========================================================
    [Setup]          Setup Testcase                  
                     Run IVR Rerouting Call And Check Nonexistent Ticket   
    [Teardown]       Teardown Testcase
